
buildscript {
    ext.kotlin_version = '1.1.1'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

// use -PtargetEnv=js : ./gradlew -PtargetEnv=js :briRestClient:assemble
// then look at briRestClient/javascript/test.html
def js = (project.hasProperty("targetEnv") && targetEnv == "js")
if (js) {
    logger.warn("configured for javascript")
    apply plugin: 'kotlin2js'
}else {
    logger.warn("configured for jvm")
    apply plugin: 'kotlin'
}
apply plugin: 'maven'

version = "0.0.1"
group = 'biz.bluerain.bri'
version = "$version-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    if (js) {
        compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    }else{
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    }
    compile group: 'org.json', name: 'json', version: '20160810'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    main.kotlin.srcDirs += 'src/shared'
    if (js) {
        main.kotlin.srcDirs += 'src/js'
    }else{
        main.kotlin.srcDirs += 'src/jvm'
    }
    test.kotlin.srcDirs += 'src/test'
}

if (js) {
    compileKotlin2Js {
        kotlinOptions.outputFile = "javascript/kh.js"
        kotlinOptions.moduleKind = "plain" // amd?
        kotlinOptions.sourceMap = true
    }
}
